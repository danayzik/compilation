/*************/
/* IMPORT(S) */
/*************/

import java_cup.runtime.*;
import AST.*;


parser code 
{:

	public Lexer lexer;
	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getTokenStartPosition());
		System.out.print("] ");		
		throw new SyntaxError(String.valueOf(lexer.getLine()));
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	if (s.sym == TokenNames.ERROR)
	    throw new LexicalError("");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/

terminal EQ;
terminal LT;
terminal GT;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal ASSIGN;
terminal TYPE_INT;
terminal TYPE_STRING;
terminal TYPE_VOID;
terminal COMMA;
terminal ARRAY;
terminal NIL;
terminal NEW;
terminal EXTENDS;
terminal RETURN;
terminal ERROR;



/*************/
/* TERMINALS */
/*************/
terminal Integer SEMICOLON;
terminal Integer INT;
terminal String ID;
terminal String STRING;
terminal Integer CLASS;
terminal Integer LPAREN;
terminal Integer IF;
terminal Integer WHILE;
/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_DEC dec;
non terminal AST_TYPE type;
non terminal AST_CFIELD cField;
non terminal AST_NEW_EXP newExp;
non terminal AST_STMT stmt;
non terminal AST_STMT_LIST stmtList;
non terminal AST_FUNC_ARG_LIST funcArgList;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_EXP_LIST expList;
non terminal AST_DEC_LIST decList;
non terminal AST_PROGRAM program;


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence nonassoc ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence nonassoc LBRACK;
precedence nonassoc LPAREN;
precedence left DOT;

/************************/
/* START WITH: stmtList */
/************************/
start with program;

/********************/
/* DERIVATION RULES */
/********************/

program	::=		    decList:l		{: RESULT = new AST_PROGRAM(l);    :}
					;

decList	::=		    dec:d	decList:l		{: RESULT = new AST_DEC_LIST(d,l);    :}
					| dec:d				{: RESULT = new AST_DEC_LIST(d,null); :}
					;

stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AST_STMT_LIST(s,l);    :}
					| stmt:s				{: RESULT = new AST_STMT_LIST(s,null); :}
					;

cFieldList	::=		cField:f	cFieldList:l		{: RESULT = new AST_CFIELD_LIST(f,l);    :}
					| cField:f				{: RESULT = new AST_CFIELD_LIST(f,null); :}
					;

expList	::=		    COMMA exp:e expList:l		{: RESULT = new AST_EXP_LIST(e,l);    :}
					| COMMA exp:e				{: RESULT = new AST_EXP_LIST(e,null); :}
					;

var			::=		ID:name								{: RESULT = new AST_VAR_SIMPLE(parser.lexer.getLine(), name);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AST_VAR_FIELD(parser.lexer.getLine(), v,fieldName); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AST_VAR_SUBSCRIPT(parser.lexer.getLine(), v,e);     :}
					;
					
stmt		::=		  type:t ID:id ASSIGN newExp:e SEMICOLON:line				    {: RESULT = new AST_LOCAL_VAR_DEC(line, t, id, e); :}
                    | type:t ID:id SEMICOLON:line	                                {: RESULT = new AST_LOCAL_VAR_DEC(line, t, id); :}
                    | type:t ID:id ASSIGN exp:e SEMICOLON:line                    {: RESULT = new AST_LOCAL_VAR_DEC(line, t, id, e); :}
                    | var:v ASSIGN exp:e SEMICOLON:line								{: RESULT = new AST_STMT_ASSIGN(line, v,e); :}
                    | var:v ASSIGN newExp:e SEMICOLON:line								{: RESULT = new AST_STMT_ASSIGN(line, v,e); :}
                    | RETURN SEMICOLON:line                                      {: RESULT = new AST_STMT_RETURN(line, null); :}
                    | RETURN exp:e SEMICOLON:line                                      {: RESULT = new AST_STMT_RETURN(line, e); :}
					| IF:line LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(line, cond,body); :}
					| WHILE:line LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(line, cond,body); :}
					| ID:funcID LPAREN:line RPAREN SEMICOLON          {: RESULT = new AST_STMT_FUNC_CALL(line, funcID, null);:}
					| ID:funcID LPAREN:line exp:e expList:argList RPAREN SEMICOLON          {: RESULT = new AST_STMT_FUNC_CALL(line, funcID, new AST_EXP_LIST(e, argList));:}
					| var:v DOT ID:funcID LPAREN:line RPAREN SEMICOLON          {: RESULT = new AST_STMT_FUNC_CALL(line, v, funcID, null);:}
                    | ID:funcID LPAREN:line exp:e RPAREN SEMICOLON          {: RESULT = new AST_STMT_FUNC_CALL(line, funcID, new AST_EXP_LIST(e,null));:}
					| var:v DOT ID:funcID LPAREN:line exp:e RPAREN SEMICOLON  {: RESULT = new AST_STMT_FUNC_CALL(line, v, funcID, new AST_EXP_LIST(e,null));:}
                    | var:v DOT ID:funcID LPAREN:line exp:e expList:argList RPAREN SEMICOLON  {: RESULT = new AST_STMT_FUNC_CALL(line, v, funcID, new AST_EXP_LIST(e, argList));:}
					;

exp			::=		INT:i						        {: RESULT = new AST_EXP_INT(parser.lexer.getLine(), i); :}
                    | MINUS INT:i						{: RESULT = new AST_EXP_INT(parser.lexer.getLine(), -i); :}
					| var:v								{: RESULT = new AST_EXP_VAR(parser.lexer.getLine(), v); :}
					| LPAREN exp:e1 RPAREN				{: RESULT = new AST_EXP_PAREN(parser.lexer.getLine(), e1); :}
					| exp:e1 PLUS  exp:e2				{: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 0);:}
					| exp:e1 MINUS exp:e2				{: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 1);:}
					| exp:e1 TIMES  exp:e2				{: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 2);:}
					| exp:e1 DIVIDE  exp:e2				{: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 3);:}
					| exp:e1 LT  exp:e2				    {: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 4);:}
					| exp:e1 GT  exp:e2				    {: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 5);:}
					| exp:e1 EQ  exp:e2				    {: RESULT = new AST_EXP_BINOP(parser.lexer.getLine(), e1, e2, 6);:}
					| STRING:s1				            {: RESULT = new AST_EXP_STRING(parser.lexer.getLine(), s1.substring(1, s1.length() - 1));:}
					| NIL			                    {: RESULT = new AST_EXP_NIL(parser.lexer.getLine());:}
					| ID:funcID LPAREN:line RPAREN           {: RESULT = new AST_EXP_FUNC_CALL(line, funcID, null);:}
					| ID:funcID LPAREN:line exp:e expList:argList RPAREN           {: RESULT = new AST_EXP_FUNC_CALL(line, funcID, new AST_EXP_LIST(e, argList));:}
					| var:v DOT ID:funcID LPAREN:line RPAREN           {: RESULT = new AST_EXP_FUNC_CALL(line, v, funcID, null);:}
                    | ID:funcID LPAREN:line exp:e RPAREN           {: RESULT = new AST_EXP_FUNC_CALL(line, funcID, new AST_EXP_LIST(e,null));:}
					| var:v DOT ID:funcID LPAREN:line exp:e RPAREN   {: RESULT = new AST_EXP_FUNC_CALL(line, v, funcID, new AST_EXP_LIST(e,null));:}
                    | var:v DOT ID:funcID LPAREN:line exp:e expList:argList RPAREN   {: RESULT = new AST_EXP_FUNC_CALL(line, v, funcID, new AST_EXP_LIST(e, argList));:}
					;

dec		  ::=		  type:t ID:id ASSIGN newExp:e SEMICOLON:line				    {: RESULT = new AST_GLOBAL_VAR_DEC(line, t, id, e); :}
                    | type:t ID:id SEMICOLON:line	                                {: RESULT = new AST_GLOBAL_VAR_DEC(line, t, id); :}
                    | type:t ID:id ASSIGN exp:e SEMICOLON:line	                    {: RESULT = new AST_GLOBAL_VAR_DEC(line, t, id, e); :}
					| ARRAY ID:id EQ type:t LBRACK RBRACK SEMICOLON:line	            {: RESULT = new AST_DEC_ARRAY_TYPE_DEF(line, id, t); :}
					| type:funcType ID:funcName LPAREN:line RPAREN LBRACE stmtList:body RBRACE {: RESULT = new AST_GLOBAL_FUNC_DEC(line, funcType, funcName, body, null); :}
                    | type:funcType ID:funcName LPAREN:line type:argType ID:argID RPAREN LBRACE stmtList:body RBRACE  {: RESULT = new AST_GLOBAL_FUNC_DEC(line, funcType, funcName, body, new AST_FUNC_ARG_LIST(new AST_FUNC_ARG(parser.lexer.getLine(), argType, argID), null)); :}
                    | type:funcType ID:funcName LPAREN:line type:argType ID:argID funcArgList:fArgLst RPAREN LBRACE stmtList:body RBRACE  {: RESULT = new AST_GLOBAL_FUNC_DEC(line, funcType, funcName, body, new AST_FUNC_ARG_LIST(new AST_FUNC_ARG(parser.lexer.getLine(), argType, argID), fArgLst)); :}
					| CLASS:line ID:id LBRACE cFieldList:flist RBRACE {: RESULT = new AST_CLASS_DEC(line, id, flist, null); :}
					| CLASS:line ID:id EXTENDS ID:parentID LBRACE cFieldList:flist RBRACE  {: RESULT = new AST_CLASS_DEC(line, id, flist, parentID); :}
					;

type	::=		    TYPE_INT								{: RESULT = new AST_TYPE(parser.lexer.getLine(), "int"); :}
					| TYPE_STRING 	                        {: RESULT = new AST_TYPE(parser.lexer.getLine(), "string"); :}
					| TYPE_VOID	                            {: RESULT = new AST_TYPE(parser.lexer.getLine(), "void"); :}
					| ID:s	                                {: RESULT = new AST_TYPE(parser.lexer.getLine(), s); :}
					;

newExp	::=		    NEW type:t1								{: RESULT = new AST_NEW_EXP_SIMPLE(parser.lexer.getLine(), t1); :}
					| NEW type:t LBRACK exp:e RBRACK 	    {: RESULT = new AST_NEW_EXP_ARRAY(parser.lexer.getLine(), t, e); :}
					;

cField	::=		      type:t ID:id ASSIGN newExp:e SEMICOLON:line				    {: RESULT = new AST_CLASS_VAR_DEC(line, t, id, e); :}
                    | type:t ID:id SEMICOLON:line                               {: RESULT = new AST_CLASS_VAR_DEC(line, t, id); :}
                    | type:t ID:id ASSIGN exp:e SEMICOLON:line	                    {: RESULT = new AST_CLASS_VAR_DEC(line, t, id, e); :}
					| type:funcType ID:funcName LPAREN:line RPAREN LBRACE stmtList:body RBRACE {: RESULT = new AST_CLASS_FUNC_DEC(line, funcType, funcName, body, null); :}
                    | type:funcType ID:funcName LPAREN:line type:argType ID:argID RPAREN LBRACE stmtList:body RBRACE  {: RESULT = new AST_CLASS_FUNC_DEC(line, funcType, funcName, body, new AST_FUNC_ARG_LIST(new AST_FUNC_ARG(parser.lexer.getLine(), argType, argID), null)); :}
                    | type:funcType ID:funcName LPAREN:line type:argType ID:argID funcArgList:fArgLst RPAREN LBRACE stmtList:body RBRACE  {: RESULT = new AST_CLASS_FUNC_DEC(line, funcType, funcName, body, new AST_FUNC_ARG_LIST(new AST_FUNC_ARG(parser.lexer.getLine(), argType, argID), fArgLst)); :}
					;


funcArgList	 ::=	COMMA type:argType ID:argID funcArgList:fArgLst				{: RESULT = new AST_FUNC_ARG_LIST(new AST_FUNC_ARG(parser.lexer.getLine(),argType,argID), fArgLst); :}
                    | COMMA type:argType ID:argID                                {: RESULT = new AST_FUNC_ARG_LIST(new AST_FUNC_ARG(parser.lexer.getLine(),argType,argID), null); :}
					;




